/*
    Shows the different response types
*/
require("lib/web", "web")

var app = web:newWebApp({autoReload->true})

//   GET http://localhost:8121/text
app.route

    ({path -> "/text"},
    def(context) {            
        return web:ok().text("This is plain old text.")
    })

//   GET http://localhost:8121/html    
app.route

    ({path -> "/html"},
    def(context) {            
        return web:ok().html(""" 
            <html>
                <head>
                    <title>This is a test</title>
                </head>
                <body>
                    <div>
                        Hello world from inlined <b>HTML</b>
                    </div>
                </body>
            </html>
        """)
    })    
   
//   GET http://localhost:8121/json    
app.route

    ({path -> "/json"},
    def(context) {           
        var json = {
            array -> [1,3,"brett"],
            map -> {
                key -> "value",
                key2 -> true
            }
        }
        return web:ok().json(json)
    })  


//   GET http://localhost:8121/redirect    
app.route

    ({path -> "/redirect"},
    def(context) {                   
        return web:redirect("/json")
    })  


//   GET http://localhost:8121/template
app.route

    ({path -> "/template"},
    def(context) {               
        var templateParams = {
            bestQB -> "Brett f'ing Favre",
            bestTeam -> "Green Bay Packers"
        }
        return web:ok().template("/template.html", templateParams)
    })  
    
    

//   GET http://localhost:8121/status?code=200
app.route

    ({path -> "/status"},
    def(context) {               
        var statusCode = context.param("code")
        return web:status( toInt(statusCode) ).json({
            paramStatusCode -> statusCode
        })
    })  
    
        
    
app.start()